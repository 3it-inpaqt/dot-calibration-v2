.SUBCKT OTA1 + - ABC V+ V- out params: n=2 VBE=0.65 Rin=1e10 VA=75 B=100 VTi=0.0025
R1 + 1 0.01
R2 - 2 0.01
Ri 1 2 {Rin}
R3 ABC 3 0.01 
VABC 3 4 0
V1 4 V- {n*VBE}
Gro1 out V+ value={(I(VABC)*V(out, V+))/(2*B*VA)}
Gro2 out V- value={(I(VABC)*V(out, V-))/(2*B*VA)}
G1 V+ out value={0.5*I(VABC)*(exp(v(1,2)/(2*VTi))-exp(v(2,1)/(2*VTi)))/(exp(v(1,2)/(2*VTi))+exp(v(2,1)/(2*VTi)))}
G2 V- out value={0.5*I(VABC)*(exp(v(1,2)/(2*VTi))-exp(v(2,1)/(2*VTi)))/(exp(v(1,2)/(2*VTi))+exp(v(2,1)/(2*VTi)))}
.ends OTA1

*
* OPAMP MACRO MODEL, SINGLE-POLE 
* connections:      non-inverting input
*                   |   inverting input
*                   |   |   output
*                   |   |   |
.SUBCKT OPAMP1      1   2   6
* INPUT IMPEDANCE
RIN	1	2	500MEG
* DC GAIN (100K) AND POLE 1 (10HZ)
EGAIN	3 0	1 2	100K
RP1	3	4	1K
CP1	4	0	0.5UF
* OUTPUT BUFFER AND RESISTANCE
EBUFFER	5 0	4 0	1
ROUT	5	6	10
.ENDS OPAMP1

.SUBCKT TIA      1   2   6
* INPUT IMPEDANCE
RIN	1	2	500MEG
* DC GAIN (100K) AND POLE 1 (10HZ)
EGAIN	3 0	1 2	100K
RP1	3	4	1K
CP1	4	0	1pF
* OUTPUT BUFFER AND RESISTANCE
EBUFFER	5 0	4 0	1
ROUT	5	6	10
.ENDS TIA